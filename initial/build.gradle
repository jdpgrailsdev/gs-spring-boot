buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.2.2.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

bootJar {
    baseName = 'gs-spring-boot'
    version =  '0.1.0'
}

configurations {
    intTestCompile.extendsFrom testCompile
    intTestRuntime.extendsFrom intTestCompile
    all {
        exclude module: 'slf4j-log4j12'
    }
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.integration:spring-integration-core"
    compile "org.springframework:spring-messaging"
    compile "org.slf4j:slf4j-api"

    testCompile "junit:junit"
    testCompile "cglib:cglib:3.3.0"
    testCompile "org.mockito:mockito-all:1.10.19"
    testCompile "org.objenesis:objenesis:3.1"
    testCompile "org.codehaus.groovy:groovy-all:2.5.8"
    testCompile ("org.spockframework:spock-core:1.3-groovy-2.5") {
        exclude module: 'groovy-all'
    }

    intTestCompile "org.spockframework:spock-spring:1.3-groovy-2.5"
    intTestCompile "org.springframework:spring-test"
    intTestCompile "org.springframework.boot:spring-boot-test"

    intTestRuntime "org.apache.kafka:kafka_2.12:0.10.2.1"
    intTestRuntime "org.springframework.kafka:spring-kafka-test:1.2.3.RELEASE"
}

sourceSets {
    intTest {
        groovy {
            srcDir file('src/intTest/groovy')
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

test {
    include '**/*Spec*'
    include '**/*Test*'
}

task intTest(type:Test) {
    description = "Run integration tests (located in src/intTest/...)."
    testClassesDirs = project.sourceSets.intTest.output.classesDirs
    classpath = project.sourceSets.intTest.runtimeClasspath
    reports.html.destination = file("$reports.html.destination/integration")
    reports.junitXml.destination = file("$reports.junitXml.destination/integration")
}

check.dependsOn intTest
intTest.mustRunAfter test